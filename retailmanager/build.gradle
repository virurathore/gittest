buildscript {
  ext {
        springBootVersion = '1.3.5.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repo.spring.io/snapshot' }
        maven { url 'http://repo.spring.io/milestone' } 
        maven { url 'http://repo.spring.io/libs-release' }
		maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
       classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
       classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
	   classpath 'com.eriwen:gradle-js-plugin:2.14.1'
    }
}
apply plugin: 'com.eriwen.gradle.js'
apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: "io.spring.dependency-management"
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

bootRepackage { 
	classifier = 'exec' 
}

jar {
	baseName = 'retailmanager'
	version = '0.0.1-SNAPSHOT'
}

war {
	baseName = 'retailmanager'
	version = '0.0.1-RELEASE'
}

group = 'org.retailmanager'
version = '0.0.1-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
  checkstyleVersion = '6.16.1'
  pmdVersion = '5.4.1'
}


repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
	compile group: 'org.springframework.boot', name: 'spring-boot-starter'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'com.google.maps', name: 'google-maps-services', version:'0.1.15'
	testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

dependencyManagement {
    imports { 
        mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.RELEASE" 
    }
}

checkstyle {
  toolVersion = checkstyleVersion
  config = rootProject.resources.text.fromFile('etc/checkstyle/google_checks.xml')
}

pmd {
  toolVersion = pmdVersion
  ruleSetConfig = rootProject.resources.text.fromFile('etc/pmd/ruleset.xml')
  ignoreFailures = true
}


// Declare your sources
javascript.source {
    dev {
        js {
            srcDir 'src/main/resources/static'
            include "*.js"
            exclude "*.min.js"
        }
    }
    prod {
        js {
            srcDir 'src/main/resources/static'
            include "*.min.js"
        }
    }
}

// Configure the built-in task
//https://github.com/eriwen/gradle-js-plugin
combineJs {
    encoding = "UTF-8"
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/retailmanager.js")
}

minifyJs {
	source = combineJs
    dest = file("${buildDir}/resources/main/static/retailmanager-min.js")
    sourceMap = file("${buildDir}/retailmanager.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
    }
}

check.finalizedBy projectReport

projectReport.finalizedBy buildDashboard
